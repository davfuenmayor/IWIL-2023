(**************************************************************)
(*                  Modal correspondence theory.              *)
(* If you spot any mistakes please report them to the author: *)
(*    David Fuenmayor - david.fuenmayor@uni-bamberg.de)       *)
(**************************************************************)
theory "modal-correspondence"
  imports endorel "set-operation"
begin

(*A (generalized) definition of order embedding (allowing for different orderings in the domain & codomain)*)
abbreviation order_embedding::"('a,'a)\<rho> \<Rightarrow> ('b,'b)\<rho> \<Rightarrow> ('a \<Rightarrow>'b) \<Rightarrow> bool" ("_,_-EMBD")
  where "o1,o2-EMBD \<phi> \<equiv> \<forall>A B. (o1 A B) \<longleftrightarrow> (o2 (\<phi> A) (\<phi> B))"

lemma preimage_order: "(\<subseteq>\<^sup>r),(\<subseteq>\<^sup>r)-EMBD preimage" by (smt (verit, best) preimage_char preimage_def subset_def subset_overlap_char)
lemma image_order: "(\<subseteq>\<^sup>r),(\<subseteq>\<^sup>r)-EMBD image" by (smt (verit, ccfv_threshold) preimage_order image_transp1 rel_transpose_subset)
lemma dual_preimage_order: "(\<subseteq>\<^sup>r),(\<subseteq>\<^sup>r)\<Zcat>-EMBD dpreimage" by (smt (verit, best) dpreimage_char subset_def)
lemma dual_image_order: "(\<subseteq>\<^sup>r),(\<subseteq>\<^sup>r)\<Zcat>-EMBD dimage" by (smt (verit, ccfv_threshold) dimage_transp1 dual_preimage_order rel_transpose_subset)

lemma image_id:    "ID\<^sup>r,ID-MORPH image" unfolding image_def by simp
lemma preimage_id: "ID\<^sup>r,ID-MORPH preimage" unfolding preimage_def by simp
lemma dimage_id: "ID\<^sup>r,ID-MORPH dimage" unfolding dimage_def by simp
lemma dpreimage_id: "ID\<^sup>r,ID-MORPH dpreimage" unfolding dpreimage_def by simp

lemma image_comp:    "(\<circ>\<^sup>r),(\<circ>)-MORPH image" unfolding image_def by (metis (no_types, opaque_lifting) rcomp_def)
lemma preimage_comp: "(\<circ>\<^sup>r),(\<circ>)\<Zcat>-MORPH preimage" unfolding preimage_def by (metis (no_types, opaque_lifting) rcomp_def)
lemma dimage_comp: "(\<circ>\<^sup>r),(\<circ>)-MORPH dimage" unfolding dimage_def by (metis (no_types, opaque_lifting) rcomp_def)
lemma dpreimage_comp: "(\<circ>\<^sup>r),(\<circ>)\<Zcat>-MORPH dpreimage" unfolding dpreimage_def by (metis (no_types, opaque_lifting) rcomp_def)

lemma residuation1: "RESID (image R) (dpreimage R)" by (smt (verit, del_insts) dpreimage_def image_def subset_def)
lemma residuation2: "RESID (preimage R) (dimage R)" by (smt (verit) dimage_def preimage_def subset_def)

typedecl i
consts R::"(i,i)\<rho>"

abbreviation box ("\<^bold>\<box>")
  where "\<^bold>\<box>A \<equiv> dpreimage R A"
abbreviation dia ("\<^bold>\<diamond>")
  where "\<^bold>\<diamond>A \<equiv> preimage R A"
abbreviation box' ("\<^bold>\<box>''")
  where "\<^bold>\<box>'A \<equiv> dimage R A"
abbreviation dia' ("\<^bold>\<diamond>''")
  where "\<^bold>\<diamond>'A \<equiv> image R A"

(* lemma residuation1: "RESID \<^bold>\<diamond>' \<^bold>\<box>" by (smt (verit, del_insts) dpreimage_def image_def subset_def) *)
(* lemma residuation2: "RESID \<^bold>\<diamond> \<^bold>\<box>'" by (smt (verit) dimage_def preimage_def subset_def) *)

lemma trans_corresp:  "transitive R \<longleftrightarrow> (\<forall>P. (\<^bold>\<diamond>(\<^bold>\<diamond>P) \<subseteq> \<^bold>\<diamond>P))"
  by (metis preimage_comp Indirect_def2 preimage_order transitive_reldef)
lemma trans_corresp': "transitive R \<longleftrightarrow> (\<forall>P. (\<^bold>\<box>P \<subseteq> (\<^bold>\<box>(\<^bold>\<box>P))))"
  by (metis Indirect_def2 dpreimage_comp dual_preimage_order transitive_reldef)

lemma refl_corresp:  "reflexive R \<longleftrightarrow> (\<forall>P. (P \<subseteq> \<^bold>\<diamond>P))" by (metis preimage_id preimage_order reflexive_reldef)
lemma refl_corresp': "reflexive R \<longleftrightarrow> (\<forall>P. (\<^bold>\<box>P \<subseteq> P))" by (metis Reflexive_def dpreimage_char dpreimage_id dual_preimage_order)

lemma symm_corresp: "symmetric R \<longleftrightarrow> (\<forall>P. (P \<subseteq> \<^bold>\<box>(\<^bold>\<diamond>P)))"
  by (metis image_order image_transp2 rel_transpose_equ residuation1 symmetric_reldef)
lemma dense_corresp: "dense R \<longleftrightarrow> (\<forall>P. (\<^bold>\<box>(\<^bold>\<box>P) \<subseteq> \<^bold>\<box>P))"
  by (metis AllR_def Dense_reldef Indirect_def2 dpreimage_comp dual_preimage_order subset_setdef)

lemma euclidean_corresp: "euclideanR R \<longleftrightarrow> (\<forall>P. \<^bold>\<diamond>P \<subseteq> \<^bold>\<box>(\<^bold>\<diamond>P))"
  by (metis Downward_def2 euclideanR_reldef image_transp1 preimage_comp preimage_order residuation1)

(*We discuss three wormholes. The first is motivational (serves to illustrate astronomical
magnitudes involved). Second and thirst correspond to applications of logic in CS and math
 and are less exotic. Serve to illustrate usefulness of algebraic DSL for cut intro*)

end
